apply plugin: 'com.android.library'

def gitHead = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))

android {

    compileSdkVersion bfcBuildConfig.compileSdkVersion
    buildToolsVersion "25.0.0"

    defaultConfig {

        minSdkVersion bfcBuildConfig.minSdkVersion
        targetSdkVersion bfcBuildConfig.targetSdkVersion
        versionCode VERSION_CODE
        versionName VERSION_NAME

        buildConfigField "String", "GIT_HEAD", "\"${gitHead}\""
        buildConfigField "String", "GIT_TAG", "\"${gitTag}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "LIBRARY_NAME", "\"${POM_ARTIFACT_ID}\""

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android.libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('-release.aar')) {
                def fileName = POM_ARTIFACT_ID + "-" + VERSION_NAME + ".aar";
                output.outputFile = new File(outputFile.parent, fileName)
                println "output： " + output.outputFile
            }

            variant.assemble.doLast{
                def jarName = POM_ARTIFACT_ID + "-" + VERSION_NAME + ".jar"

                def fromFile = "${project.buildDir}/intermediates/bundles/default/classes.jar"
                def intoFile = "${project.buildDir}/outputs/jar/"

                copy {
                    from fromFile
                    into intoFile
                    rename {
                        jarName
                    }
                }
                println("===============================================================")
                println("PREPARE COPY JAR TO FOLDER: " + intoFile + jarName)
                println("===============================================================")
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

// 导入上传Library到Maven仓库的配置
apply from:"http://172.28.2.93/bfc/Bfc/raw/develop/common-config/maven-push-jar.gradle"

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile bfcBuildConfig.deps.'support-annotations'
}